apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
group = 'com.king.tratt'
archivesBaseName = 'tratt-api'

version='0.1.1-SNAPSHOT'

task wrapper(type: Wrapper) {
   gradleVersion = '2.10' //version required
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.google.code.gson:gson:2.6.2'
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.jmockit:jmockit:1.24'
    testCompile 'org.assertj:assertj-core:3.4.1'
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Tratt-API'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A QA tool for testing an event tracking system on an end-to-end level.'
        url 'https://github.com/king/tratt-api'

        scm {
          connection 'scm:git:https://github.com/king/tratt-api.git'
          developerConnection 'scm:git:https://github.com/king/tratt-api.git'
          url 'https://github.com/king/tratt-api'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'alexander-poulikakos'
            name 'Alexander Poulikakos'
            email 'alexander.poulikakos@king.com'
          }
        }
      }
    }
  }
}
